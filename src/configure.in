dnl ***************************************************************************
dnl *** configure.in for libgnomekbd                                        ***
dnl ***************************************************************************
AC_INIT(libgnomekbd, 2.28.2, [http://bugzilla.gnome.org/enter_bug.cgi?product=libgnomekbd])
AC_PREREQ(2.59)

VERSION_INFO=4:0:0
AC_SUBST(VERSION_INFO)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE([1.9])
AC_CONFIG_MACRO_DIR(m4)

dnl ***************************************************************************
dnl *** Minimum library versions for libgnomekbd                            ***
dnl ***************************************************************************
GLIB_REQUIRED=2.16
DBUS_REQUIRED=0.92
DBUS_GLIB_REQUIRED=0.34
GCONF_REQUIRED=2.14.0
GDK_REQUIRED=2.13
GTK_REQUIRED=2.13
LIBXKLAVIER_REQUIRED=4.0

AC_SUBST([DBUS_REQUIRED])
AC_SUBST([DBUS_GLIB_REQUIRED])
AC_SUBST([GCONF_REQUIRED])
AC_SUBST([GDK_REQUIRED])
AC_SUBST([GTK_REQUIRED])
AC_SUBST([LIBXKLAVIER_REQUIRED])

dnl ***************************************************************************

GNOME_COMMON_INIT
GNOME_COMPILE_WARNINGS

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL

# ****
# i18n
# ****

GETTEXT_PACKAGE=libgnomekbd
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [Gettext package])

AM_GLIB_GNU_GETTEXT

IT_PROG_INTLTOOL([0.35.0])

# *****************************************************************************

AC_CHECK_FUNCS(setlocale)

PKG_CHECK_MODULES(GLIB, glib-2.0 >= $GLIB_REQUIRED)

AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

PKG_CHECK_MODULES(DBUS, dbus-1 >= $DBUS_REQUIRED dbus-glib-1 >= $DBUS_GLIB_REQUIRED)

AC_SUBST(DBUS_CFLAGS)
AC_SUBST(DBUS_LIBS)

PKG_CHECK_MODULES(GCONF, gconf-2.0 >= $GCONF_REQUIRED)

AC_SUBST(GCONF_CFLAGS)
AC_SUBST(GCONF_LIBS)

PKG_CHECK_MODULES(GDK, gdk-2.0 >= $GDK_REQUIRED)

AC_SUBST(GDK_CFLAGS)
AC_SUBST(GDK_LIBS)

PKG_CHECK_MODULES(GTK, gtk+-2.0 >= $GTK_REQUIRED gdk-x11-2.0 >= $GDK_REQUIRED)

AC_SUBST(GTK_CFLAGS)
AC_SUBST(GTK_LIBS)

PKG_CHECK_MODULES(LIBXKLAVIER, libxklavier >= $LIBXKLAVIER_REQUIRED)

AC_SUBST(LIBXKLAVIER_CFLAGS)
AC_SUBST(LIBXKLAVIER_LIBS)

AC_PATH_XTRA
XLIB_CFLAGS="$X_CFLAGS"
XLIB_LIBS="$X_LIBS -lX11 $X_EXTRA_LIBS"
AC_SUBST(XLIB_CFLAGS)
AC_SUBST(XLIB_LIBS)

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

AC_ARG_ENABLE([tests],
              AC_HELP_STRING([--disable-tests], [Dont build the test apps]),,
              [enable_tests=yes])

AM_CONDITIONAL(BUILD_TESTS, test "$enable_tests" = "yes")

AC_OUTPUT([
Makefile
libgnomekbd.pc
libgnomekbdui.pc
po/Makefile.in
libgnomekbd/Makefile
test/Makefile
capplet/Makefile
capplet/gkbd-indicator-plugins-capplet.desktop.in
])
